apply plugin: 'com.android.library'
apply from: './quality.gradle'

ext.mavenPublishProperties = [
        group      : "co.infinum",
        version    : versions.queen,
        artifact   : "queen-of-versions",
        repository : [
                url     : rootProject.ext.sonatype.url,
                username: sonatype.username,
                password: sonatype.password
        ],
        name       : "Queen of Versions",
        description: "Library that checks update availability using In-App updates",
        url        : "https://github.com/infinum/Android-Prince-of-Versions",
        scm        : [
                connection: "https://github.com/infinum/Android-Prince-of-Versions.git",
                url       : "https://github.com/infinum/Android-Prince-of-Versions"
        ]
]

android {
    compileSdkVersion buildVersions.compile

    defaultConfig {
        minSdkVersion buildVersions.min
        targetSdkVersion buildVersions.target
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

version = ext.mavenPublishProperties.version
group = ext.mavenPublishProperties.group

dependencies {
    api project(path: ':prince-of-versions')
    implementation 'com.google.android.play:app-update:2.1.0'
    implementation deps.appcompat
    compileOnly deps.fbannotations

    testImplementation deps.junit
    testImplementation deps.mockito
    testImplementation deps.mockwebserver
    testImplementation deps.assertj
    testImplementation deps.json
    testCompileOnly deps.fbannotations
}

apply from: '../tasks.gradle'
apply from: '../maven-publish.gradle'

preBuild.dependsOn ':prince-of-versions:generateReadme'
