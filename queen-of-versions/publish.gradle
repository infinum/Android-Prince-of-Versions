apply plugin: "maven-publish"
apply plugin: "signing"

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                name sonatype.name()
                url sonatype.url()
                credentials {
                    username sonatype.username()
                    password sonatype.password()
                }
            }
        }
        publications {
            release(MavenPublication) {
                groupId = publishedGroupId
                version = versions.queen

                artifactId = publishedArtifactName

                artifact bundleReleaseAar
                artifact sourcesJar
                artifact javadocsJar

                pom {
                    name = 'Queen of Versions'
                    description = 'Library that checks update availability using In-App updates'
                    url = 'https://github.com/infinum/Android-Prince-of-Versions'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'NikoPelicaric'
                            name = 'Niko Pelicaric'
                            email = 'niko.pelicaric@infinum.com'
                        }
                    }
                    scm {
                        connection = 'https://github.com/infinum/Android-Prince-of-Versions.git'
                        developerConnection = 'https://github.com/infinum/Android-Prince-of-Versions.git'
                        url = 'https://github.com/infinum/Android-Prince-of-Versions'
                    }
                }
                pom.withXml {
                    def root = asNode()
                    def dependenciesNode = root.appendNode('dependencies')
                    configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
                signing {
                    sign publishing.publications.release
                }
            }
        }
    }
}