task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.source
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    excludes = ['**/*.kt']
}

afterEvaluate {
    javadocs.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompile.classpath.files
    })
}

private void replaceVersionsInFile(File file) {
    def content = file.text
    content = content.replaceAll("'co\\.infinum:prince-of-versions:.+?'", "'co.infinum:prince-of-versions:${versions.prince}'")
    content = content.replaceAll("'co\\.infinum:queen-of-versions:.+?'", "'co.infinum:queen-of-versions:${versions.queen}'")
    file.setText(content)
}

task generateReadme {
    doFirst {
        replaceVersionsInFile(new File("${rootDir}/README.md"))
        replaceVersionsInFile(new File("${projectDir}/README.md"))
    }
}

task deploy(dependsOn: [
        'clean',
        'publishReleasePublicationToSonatypeMavenCentralRepository'
]) {
    group = "Deploy"
    description = "Deploy module to repositories"
}
